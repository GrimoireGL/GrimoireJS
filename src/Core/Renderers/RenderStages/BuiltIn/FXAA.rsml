<?xml version="1.0" encoding="UTF-8"?>
  <rsml>
    <stage namespace="http://grimoire.gl/ns/recipe/stages" name="fxaa">
      <technique target="quad">
        <fbo primary="OUT">
          <color name="OUT" register="0"/>
        </fbo>
        <material name="jthree.basic.fxaa" group="jthree.materials.fxaa" order="300">
          <registers>
            <register name="builtin.basic" />
            <register name="builtin.buffer" />
          </registers>
          <passes>
            <pass>
              <blend enabled="false" />
              <glsl>
                <![CDATA[
            attribute vec4 position;

            varying vec2 vMUv;
            varying vec2 vSWUv;
            varying vec2 vSEUv;
            varying vec2 vNWUv;
            varying vec2 vNEUv;
            varying vec2 vUv;

            uniform mediump vec2 _resolution;

            @vert{

            void texcoords(vec2 fragCoord, vec2 resolution, out vec2 NW, out vec2 NE, out vec2 SW, out vec2 SE, out vec2 M) {
      	        vec2 inverseVP = 1.0 / resolution.xy;
      	        NW = (fragCoord + vec2(-1.0, -1.0)) * inverseVP;
      	        NE = (fragCoord + vec2(1.0, -1.0)) * inverseVP;
      	        SW = (fragCoord + vec2(-1.0, 1.0)) * inverseVP;
      	        SE = (fragCoord + vec2(1.0, 1.0)) * inverseVP;
      	        M = vec2(fragCoord * inverseVP);
              }

              void main(void)
              {
              	gl_Position = position;
                vUv = (position.xy + vec2(1.0,1.0)) * 0.5;
                texcoords(vUv * _resolution,_resolution,vNWUv,vNEUv,vSWUv,vSEUv,vMUv);
              }
            }

            @frag{

              @{type:"buffer",name:"INPUT",register:0}
              uniform sampler2D _inputBuffer;

              @{default:0.0078125}
              uniform float reduceMin;
              @{default:0.125}
              uniform float reduceMul;
              @{default:8}
              uniform float spanMax;

              vec4 fxaa(sampler2D tex, vec2 fragCoord, vec2 resolution,vec2 v_rgbNW, vec2 v_rgbNE, vec2 v_rgbSW, vec2 v_rgbSE, vec2 v_rgbM) {
                vec4 color;
                mediump vec2 inverseVP = vec2(1.0 / resolution.x, 1.0 / resolution.y);
                vec3 rgbNW = texture2D(tex, v_rgbNW).xyz;
                vec3 rgbNE = texture2D(tex, v_rgbNE).xyz;
                vec3 rgbSW = texture2D(tex, v_rgbSW).xyz;
                vec3 rgbSE = texture2D(tex, v_rgbSE).xyz;
                vec4 texColor = texture2D(tex, v_rgbM);
                vec3 rgbM  = texColor.xyz;
                vec3 luma = vec3(0.299, 0.587, 0.114);
                float lumaNW = dot(rgbNW, luma);
                float lumaNE = dot(rgbNE, luma);
                float lumaSW = dot(rgbSW, luma);
                float lumaSE = dot(rgbSE, luma);
                float lumaM  = dot(rgbM,  luma);
                float lumaMin = min(lumaM, min(min(lumaNW, lumaNE), min(lumaSW, lumaSE)));
                float lumaMax = max(lumaM, max(max(lumaNW, lumaNE), max(lumaSW, lumaSE)));

                mediump vec2 dir;
                dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
                dir.y =  ((lumaNW + lumaSW) - (lumaNE + lumaSE));

                float dirReduce = max((lumaNW + lumaNE + lumaSW + lumaSE) * 0.25 * reduceMul, reduceMin);

                float rcpDirMin = 1.0 / (min(abs(dir.x), abs(dir.y)) + dirReduce);
                dir = min(vec2(spanMax, spanMax), max(vec2(-spanMax, -spanMax), dir * rcpDirMin)) * inverseVP;

                vec4 rgbA = 0.5 * (
                texture2D(tex, fragCoord * inverseVP + dir * (1.0 / 3.0 - 0.5)) +
                texture2D(tex, fragCoord * inverseVP + dir * (2.0 / 3.0 - 0.5)));
                vec4 rgbB = rgbA * 0.5 + 0.25 * (
                texture2D(tex, fragCoord * inverseVP + dir * -0.5) +
                texture2D(tex, fragCoord * inverseVP + dir * 0.5));

                float lumaB = dot(rgbB.rgb, luma);
                if ((lumaB < lumaMin) || (lumaB > lumaMax))
                color = rgbA;
                else
                color = rgbB;
                return color;
              }

              void main(void){
                gl_FragColor = fxaa(_inputBuffer,vUv * _resolution,_resolution, vNWUv,vNEUv,vSWUv,vSEUv,vMUv);
              }
            }

            ]]>
              </glsl>
            </pass>
          </passes>
        </material>
      </technique>
    </stage>
  </rsml>
